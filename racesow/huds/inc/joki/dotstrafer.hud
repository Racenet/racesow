//

if 0 == 8 & %SHOW_SPEED

setScale #DEFAULTSCALE
setCursor 400, 300
setScale #SCALEBYWIDTH
moveCursor 0, -16
if %STRAFEANGLE > 0
	setScale #SCALEBYWIDTH

	//set size of arrows
	setSize 0.03 * 4500 - %STRAFEANGLE, 10
	
	//set color of arrows
	//default: white
	setColor 1, 1, 1, 0.8
	//negative acceleration: red
	if -5000 > %ACCELERATION
		setColor 1, 0, 0, 0.8
	endif
	//good strafe: green
	if 0.8 < %ACCELERATION / %MAX_ACCEL
		setColor 0, 1, 0, 0.8
	endif

	//dotstrafer for plasma
	if 64 & %SHOW_SPEED
	if %WEAPON_ITEM == #WEAP_PLASMAGUN

		//move cursor in position and draw arrows
		moveCursor 0.03 * %DIFF_ANGLE + %STRAFEANGLE + 9000, 0
		setAlign #LEFT, #BOTTOM
		drawPicByName gfx/hud/joki/strafearrow_right
	
		moveCursor -0.06 * %STRAFEANGLE + 9000, 0
		setAlign #RIGHT, #BOTTOM
		drawPicByName gfx/hud/joki/strafearrow_left

		//move Cursor back to the middle
		moveCursor -0.03 * %DIFF_ANGLE + %STRAFEANGLE + 9000, 0
		moveCursor 0.06 * %STRAFEANGLE + 9000, 0

	endif
	endif

	//standard dotstrafer
	//move cursor in position and draw arrows
	moveCursor 0.03 * %DIFF_ANGLE + %STRAFEANGLE , 0
	setAlign #LEFT, #BOTTOM
	drawPicByName gfx/hud/racesow/strafearrow_right

	moveCursor -0.06 * %STRAFEANGLE , 0
	setAlign #RIGHT, #BOTTOM
	drawPicByName gfx/hud/racesow/strafearrow_left

	//move Cursor back to the middle
	moveCursor -0.03 * %STRAFEANGLE + %DIFF_ANGLE, 0
	moveCursor 0.06 * %STRAFEANGLE, 0

endif
//set size,color and draw "crosshair"
setScale #NOSCALE
setSize 2, %VIDWIDTH / 80 // (800 / 80) = 10
setColor 0, 0, 0, 1
setAlign #CENTER, #BOTTOM
drawPicByName gfx/ui/white

endif

